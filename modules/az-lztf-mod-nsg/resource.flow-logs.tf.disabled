resource "azurerm_network_watcher_flow_log" "log" {
  count = var.enable_flow_log ? 1 : 0

  network_watcher_name = var.network_watcher_name
  resource_group_name  = var.network_watcher_resource_group_name

  network_security_group_id = azurerm_network_security_group.main.id
  storage_account_id        = var.storage_account_id
  enabled                   = var.storage_account_logs_enabled

  retention_policy {
    enabled = var.retention_policy_enabled
    days    = var.retention_policy_days
  }

  dynamic "traffic_analytics" {
    for_each = var.traffic_analytics_enabled ? [1] : []

    content {
      enabled               = var.traffic_analytics_enabled
      workspace_id          = var.workspace_id
      workspace_region      = var.workspace_region
      workspace_resource_id = var.workspace_resource_id
      interval_in_minutes   = var.traffic_analytics_interval_in_minutes
    }
  }

  lifecycle {
    ignore_changes = [
      tags,
    ]
  }

  version = var.flow_log_version
}
